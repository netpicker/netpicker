Value OS_TYPE (.+)
Value OS_VERSION ([^\s,]+)
Value MODEL (\d+)
Value VERSION ([\d\.]+)
Value SR_TYPE (\w+)
Value WEBSITE (http.*\.com)
Value COPYRIGHT (.*)
Value COMPILED_DATE (\d+-\d+-\d+)
Value COMPILED_TIME ([\d:]+)
Value COMPILED_TZ (\w+)
Value BUILD (\d+)
Value COMPILED_BY (\w+)
Value FIPS_MODE (\w+)
Value UPTIME (.+)
Value REBOOT_TIMESTAMP (\w+ \w+ \d+ \d+:\d+:\d+ \w+ \d+)
Value REBOOT_CAUSE (.+)

Start
  ^${OS_TYPE}\.$$ -> Version
  ^.+ -> Error

Version
  ^${OS_VERSION} \(MODEL: ${MODEL}\), Version ${VERSION} ${SR_TYPE}$$ -> Website
  ^.+ -> Error

Website
  ^Website: ${WEBSITE}$$ -> Copyright
  ^.+ -> Error

Copyright
  ^\(c\) Copyright ${COPYRIGHT}$$ -> Compiled
  ^.+ -> Error

Compiled
  ^Compiled on ${COMPILED_DATE} at ${COMPILED_TIME} ${COMPILED_TZ} \(build ${BUILD}\) by ${COMPILED_BY}$$ -> FIPS
  ^.+ -> Error

FIPS
  ^FIPS Mode :${FIPS_MODE}$$ -> Uptime
  ^.+ -> Error

Uptime
  ^AP uptime is ${UPTIME}$$ -> Reboot
  ^.+ -> Error

Reboot
  ^Reboot Time and Cause: AP rebooted ${REBOOT_TIMESTAMP}; ${REBOOT_CAUSE}$$ -> Record
  ^.+ -> Error 